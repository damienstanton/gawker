#!/usr/bin/env gawk -f

function handleErrors(inet) {
	while (inet |& getline ) {
		if (ERRNO) { print "Error: " ERRNO; exit 1 }
		print "Client: " $0;
		if ( length($0) < 1 ) break;
	}
}

function serve(inet, data, len, ors) {
	print "HTTP/1.1 200 OK"          |& inet;
	print "Content-Type: text/html"  |& inet;
	print "Server: gawker/0.1.0"     |& inet;
	print "Connection: close"        |& inet;
	print "Content-Length: " len ors |& inet;
	print data                       |& inet;
	close(inet);
}

BEGIN {
	if (ARGC < 2) { print "Usage: gawker test.html"; exit 0 }
	Counter = 1;
	while (1) {
		RS = ORS = "\r\n"
        Inet = "/inet/tcp/8080/0/0";
		getline Data < ARGV[1];
		DataSize = length(Data) + length(ORS);
		
		handleErrors(Inet)
		serve(Inet, Data, DataSize, ORS)
		
		print "Served " ARGV[1] " " Counter " times";
		Counter++;
	}
}

